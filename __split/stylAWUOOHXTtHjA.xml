<?xml version="1.0"?>
<stylesheet id="stylAWUOOHXTtHjA" lang="any" name="transform" show-in="main" webpalschematype="wpAppStylesheet"><![CDATA[<!-- (c) 2001-present All Copyrights Palomino System Innovations Inc.
Illegal reproduction and disclosure will be prosecuted  -->

<xsl:stylesheet
                version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:wp="http://www.palominosys.com/wp"
                xmlns:php="http://php.net/xsl"
                xmlns:date="http://exslt.org/dates-and-times"
                xmlns:exsl="http://exslt.org/common"
                extension-element-prefixes="exsl"
                >

  <xsl:strip-space elements="*"/>


  <!-- ===================== SEARCH ================== -->
  <!-- search query expected in $q.
the search is performed in EITHER: 
- the product-store containing current node, OR:
- the first product-store found in the web content
the template mode=product-store-search can be overridden. -->

  <xsl:template name="product-store-search">
    <xsl:apply-templates select="(ancestor-or-self::product-store | /web/pages//product-store)[1]" 
                         mode="product-store-search"/>
  </xsl:template>

  <xsl:template match="product-store" mode="product-store-search" priority="0.1">
    <h1>Results for "<xsl:value-of select="$q"/>" in store <xsl:value-of select="@name"/></h1>
    <xsl:for-each select="./products/product
                          [contains(translate(concat(title, description), 
                          'ABCDEFGHIJKLMNOPQRSTUVWXYZ', 
                          'abcdefghijklmnopqrstuvwxyz'), $q)]">
      <xsl:apply-templates select="." mode="product-store-search-result"/>
    </xsl:for-each>    
  </xsl:template>

  <xsl:template match="product" mode="product-store-search-result" priority="0.1">
    <div class="product-store-search-result">
      <h4><a href="/by-id/{@id}"><xsl:value-of select="title"/></a></h4>
      <div>
        <xsl:apply-templates select="description"/>        
      </div>
    </div>
  </xsl:template>

  <!-- category tree -->
  <xsl:template match="product-store">
    <xsl:param name="current-path"/>
    <div class="ul row product-store">
      <xsl:apply-templates select="categories/category[not(@parent)] 
                                   | categories/category[@parent='']" 
                           mode="list"/>
    </div>

  </xsl:template>

  <!--  Home page layout  -->
  <xsl:template match="product-store/categories/category" mode="homepage">


    <xsl:variable name="cat-image-src">
      <xsl:choose>
        <xsl:when test="image/@src">
          <xsl:value-of select="image/@src"/>
        </xsl:when>
        <xsl:otherwise>
          <xsl:call-template name="cat-get-product-image"/>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:variable>

    <a href="/category/{@id}">
      <div class="product">
        <div class="productContainer">
          <div class="productInner" style="background-image: url(/resource/dm/{$cat-image-src}/resize=x450);"></div>
          <div class="productCat">
            <xsl:value-of select="title"/>
          </div>
        </div>
      </div>

    </a>
  </xsl:template>




  <!--  Side navigation  -->
  <xsl:template match="product-store/categories/category" mode="navigation">

    <li class="dropdown">
      <a href="/category/{@id}"><xsl:value-of select="title"/></a>
      <ul class="dropdown-menu subnav sm-nowrap">
        <xsl:variable name="my-name" select="@name"/>
        <xsl:if test="../category[./@parent = $my-name]">
          <xsl:apply-templates select="../category[./@parent = $my-name]" mode="navigation"/>
          <xsl:variable name="name" select="@name"/>
        </xsl:if>
        <xsl:for-each select="../../products/product[@category = $my-name]">
          <li>
            <a href="/product/{@id}">
              <xsl:value-of select="title"/>
            </a>
          </li>
        </xsl:for-each>
      </ul>

    </li>
  </xsl:template>



  <!--  Product listing on a page  -->
  <xsl:template match="product-store/categories/category" mode="list">
    <xsl:variable name="my-name" select="@name"/>
    <!--     <xsl:param name="path"/> -->
    <div class="li col-lg-3 col-md-4 col-sm-6">

      <xsl:variable name="cat-image-src">
        <xsl:choose>
          <xsl:when test="image/@src">
            <xsl:value-of select="image/@src"/>
          </xsl:when>
          <xsl:otherwise>
            <xsl:call-template name="cat-get-product-image"/>
          </xsl:otherwise>
        </xsl:choose>
      </xsl:variable>

      <a href="/category/{@id}">
        <div class="product">
          <div class="productContainer">
            <div class="productImage"  style="background-image: url(/resource/dm/{$cat-image-src}/resize=x450);"></div>
            <div class="listItems">
              <div class="title">
                <xsl:value-of select="title"/>
              </div>
            </div>
          </div>
        </div>
      </a>
    </div>
  </xsl:template>



  <!-- category page -->
  <xsl:template match="product-store/categories/category">
    <xsl:variable name="my-name" select="@name"/>
    <xsl:apply-templates select="title"/>
    <xsl:apply-templates select="description"/>
    <xsl:if test="../category[./@parent = $my-name]">
      <h2>
        Sub-categories
      </h2>
      <div class="ul sub-cat nav row product-store">
        <xsl:apply-templates select="../category[./@parent = $my-name]" mode="list"/>
      </div>
    </xsl:if>
    <xsl:if test="../../products/product[@category = $my-name]">
      <h2>
        Products
      </h2>
      <div class="ul product-store row">
        <xsl:for-each select="../../products/product[@category = $my-name]">
          <div class="li col-lg-3 col-md-4 col-sm-6">

            <xsl:variable name="productImage">
              <xsl:choose>
                <xsl:when test="image/@src">
                  <xsl:value-of select="image/@src"/>
                </xsl:when>
                <xsl:otherwise>
                  <xsl:call-template name="cat-get-product-image"/>
                </xsl:otherwise>
              </xsl:choose>

            </xsl:variable>
            <a href="/product/{@id}">
              <div class="product">
                <div class="productContainer" >
                  <div class="productImage" style="background-image: url(/resource/dm/{$productImage}/resize=x450);"></div>

                  <div class="listItems">
                    <div class="title">
                      <xsl:value-of select="title"/>
                    </div>
                  </div>

                </div>
              </div>
            </a>
          </div>
        </xsl:for-each>
      </div>    
    </xsl:if>

  </xsl:template>

  <xsl:template match="product-store/categories/category/title">
    <h1><xsl:value-of select="."/></h1>
  </xsl:template>


  <xsl:template name="cat-get-product-image">
    <xsl:variable name="my-name" select="@name"/>
    <!-- for the current category, finds an appropriate image, in this order:
1. first product in this category
2. first product in a parent category
3. first product in the whole store
-->
    <xsl:choose>
      <xsl:when test="../../products/product[@category = $my-name][image]">
        <xsl:value-of select="(../../products/product[@category = $my-name])[1]/image/@src"/>
      </xsl:when>
      <xsl:when test="../category[@parent = $my-name]">
        <xsl:for-each select="../category[@parent = $my-name]">
          <xsl:call-template name="cat-get-product-image"/>
        </xsl:for-each>
      </xsl:when>
      <xsl:otherwise>
        <xsl:apply-templates select="../../products/product[1]/image/@src"/>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>




  <!-- product page -->
  <xsl:template match="product-store/products/product">
    <script> 
      function clickAndDisable(link) {
        // disable subsequent clicks
        link.onclick = function(event) {
          event.preventDefault();
        }
      }   
    </script>

    <div class="product-store-product">
      <xsl:apply-templates select="title"/>

      <div class="row">
        <div class="col-lg-7 col-md-7 col-sm-8">
          <div class="panel-group" id="accordion" role="tablist" aria-multiselectable="true">
            <xsl:if test="description != '' ">
              <div class="panel panel-default">
                <div class="panel-heading" role="tab" id="headingOne">
                  <h4 class="panel-title">
                    <a role="button" data-toggle="collapse" data-parent="#accordion" href="#collapseOne" aria-expanded="true" aria-controls="collapseOne">
                      Description
                    </a>
                  </h4>
                </div>
                <div id="collapseOne" class="panel-collapse collapse in" role="tabpanel" aria-labelledby="headingOne">
                  <div class="panel-body">
                    <xsl:apply-templates select="description"/>
                  </div>
                </div>
              </div>
            </xsl:if>
            <xsl:if test="features != ''">
              <div class="panel panel-default">
                <div class="panel-heading" role="tab" id="headingTwo">
                  <h4 class="panel-title">
                    <a class="collapsed" role="button" data-toggle="collapse" data-parent="#accordion" href="#collapseTwo" aria-expanded="true" aria-controls="collapseTwo">
                      Features
                    </a>
                  </h4>
                </div>
                <div id="collapseTwo" class="panel-collapse collapse in" role="tabpanel" aria-labelledby="headingTwo">
                  <div class="panel-body">
                    <xsl:apply-templates select="features"/>
                  </div>
                </div>
              </div>
            </xsl:if>
            <xsl:if test="file != ''">
              <div class="panel panel-default">
                <div class="panel-heading" role="tab" id="headingThree">
                  <h4 class="panel-title">
                    <a class="collapsed" role="button" data-toggle="collapse" data-parent="#accordion" href="#collapseThree" aria-expanded="false" aria-controls="collapseThree">
                      Documents
                    </a>
                  </h4>
                </div>
                <div id="collapseThree" class="panel-collapse collapse" role="tabpanel" aria-labelledby="headingThree">
                  <div class="panel-body">
                    <ul>
                      <xsl:apply-templates select="file"/>
                    </ul>
                  </div>
                </div>
              </div>
            </xsl:if>

          </div>
        </div>
        <div class="col-lg-5 col-md-5 col-sm-4">
          <div class="easyzoom easyzoom--overlay">
            <xsl:if test="./image">
              <xsl:apply-templates select="./image"/>
            </xsl:if>
          </div>
        </div>
      </div>

      <div class="row">
        <div class="col-xs-12">
          <xsl:apply-templates select="items"/>
        </div>
      </div>

      <div class="row">
        <div class="col-xs-12">
          <!--           products -->
          <!--           <xsl:apply-templates select="products" mode="similarProducts"/> -->

        </div>
      </div>

    </div>
  </xsl:template>

  <xsl:template match="product-store/products/product/file" >
    <li>
      <a href="/resource/dm/{./file/@src}" target="_blank"><xsl:value-of select="./fileName"/></a>
    </li>
  </xsl:template>

  <xsl:template match="product-store/products/product/title">
    <h2><xsl:value-of select="."/></h2>
  </xsl:template>


  <xsl:template match="product-store/products/product/items">
    <h3>Items</h3>
    <div class="table-responsive product-store-skus">
      <table class="table table-bordered">
        <tr>
          <!--           <xsl:if test="item/@name != ''"> -->
          <th>
            @if(Webpal::language() == 'fr')
            Item
            @elseif(Webpal::language() == 'en')
            Item
            @endif
          </th>
          <!--           </xsl:if> -->
          <!--           <xsl:if test="item/description != ''"> -->
          <xsl:if test="item/description[.!='']">

            <th>
              @if(Webpal::language() == 'fr')
              Description
              @elseif(Webpal::language() == 'en')
              Description
              @endif
            </th>
          </xsl:if>
          <!--           </xsl:if> -->
          <xsl:if test="item/size[.!='']">
            <th>
              @if(Webpal::language() == 'fr')
              Taille
              @elseif(Webpal::language() == 'en')
              Size
              @endif
            </th>
          </xsl:if>
          <xsl:if test="item/colour[.!='']">
            <th>
              @if(Webpal::language() == 'fr')
              Couleur
              @elseif(Webpal::language() == 'en')
              Colour
              @endif
            </th>
          </xsl:if>
          <xsl:if test="item/sterility[.!='no']">

            <th>
              @if(Webpal::language() == 'fr')
              Stérile
              @elseif(Webpal::language() == 'en')
              Sterile
              @endif
            </th>
          </xsl:if>

          <xsl:if test="item/UOM1[.!='']">
            <th>UOM1</th>
          </xsl:if>
          <xsl:if test="item/UOM2[.!='']">
            <th>UOM2</th>
          </xsl:if>
          <xsl:if test="../brand[.!='']">

            <th>
              @if(Webpal::language() == 'fr')
              Marque
              @elseif(Webpal::language() == 'en')
              Brand
              @endif
            </th>
          </xsl:if>
          <xsl:if test="item/thickness[.!='']">

            <th>
              @if(Webpal::language() == 'fr')
              Épaisseur
              @elseif(Webpal::language() == 'en')
              Thickness
              @endif
            </th>
          </xsl:if>
          <xsl:if test="item/packaging[.!='']">

            <th>
              @if(Webpal::language() == 'fr')
              Emballage
              @elseif(Webpal::language() == 'en')
              Packaging
              @endif
            </th>
          </xsl:if>
          <th></th>
        </tr>
        <xsl:apply-templates select="item[@discontinued != 'yes']"/>
      </table>
    </div>
  </xsl:template>

  <xsl:template match="product-store/products/product/items/item">

    <xsl:variable name="itemName">
      <xsl:value-of select="@name"/>
    </xsl:variable>

    <tr>
      <!--       <xsl:if test="@name != ''"> -->
      <td><xsl:value-of select="@name"/></td>
      <!--       </xsl:if> -->
      <xsl:if test="../item/description[.!='']">
        <td><xsl:value-of select="description"/></td>
      </xsl:if>
      <xsl:if test="../item/size[.!='']">
        <td><xsl:value-of select="size"/></td>
      </xsl:if>
      <xsl:if test="../item/colour[.!='']">
        <td><xsl:value-of select="colour"/></td>
      </xsl:if>
      <xsl:if test="../item/sterility[.!='no']">
        <td>
          <xsl:if test="sterility != 'no'">
            <i class="fa fa-check" aria-hidden="true"></i>
          </xsl:if>
        </td>
      </xsl:if>

      <xsl:if test="../item/UOM1[.!='']">
        <td><xsl:value-of select="UOM1"/></td>
      </xsl:if>
      <xsl:if test="../item/UOM2[.!='']">
        <td><xsl:value-of select="UOM2"/></td>
      </xsl:if>
      <xsl:if test="../../brand !=''">
        <td><xsl:value-of select="../../brand"/></td>
      </xsl:if>
      <xsl:if test="../item/thickness[.!='']">
        <td><xsl:value-of select="thickness"/></td>
      </xsl:if>
      <xsl:if test="../item/packaging[.!='']">
        <td><xsl:value-of select="packaging"/></td>
      </xsl:if>
      <td>
        <form method="post" action="/product-store/add-to-cart">
          <input type="hidden" name="skucode" value="{@name}"/>
          <input type="hidden" name="skuname" value="{@name}"/>
          <input type="hidden" name="productcode" value="{../../@name}"/>
          <input type="hidden" name="productname" value="{../../title}"/>
          <input type="hidden" name="quantity" value="1"/>
          <input type="submit" value="add to cart"/> 
        </form>
      </td>
    </tr>
  </xsl:template>

  <xsl:template match="product-store//*" priority="0.0">
    <pre>
    <xsl:copy-of select="." disable-output-escaping="yes"/>
  </pre>
  </xsl:template>

  <xsl:template match="image">
    <xsl:if test=". and @src !=''">
      <a href="/resource/dm/{@src}/resize=x1000">
        <img class="img-responsive" style="width: 100%;" src="/resource/dm/{@src}/resize=x750" />
      </a>
    </xsl:if>
  </xsl:template>



  <!-- breadcrumbs -->
  <xsl:template match="product-store/products/product" mode="breadcrumbs">
    <xsl:variable name="category" select="string(./@category)"/>
    <xsl:apply-templates select="../../categories/category[./@name = $category][1]" mode="breadcrumbs"/>
    <li class="active"><xsl:value-of select="./title[1]"/></li>
  </xsl:template>

  <xsl:template match="product-store/categories/category" mode="breadcrumbs">    
    <xsl:variable name="parent" select="./@parent"/>
    <xsl:apply-templates select="../category[./@name = $parent][1]" mode="breadcrumbs"/>
    <xsl:variable name="link">/category/<xsl:value-of select="./@id"/></xsl:variable>
    <li><a href="{$link}"><xsl:value-of select="./title[1]"/></a></li>
  </xsl:template>

  
  <xsl:template name="product-store-cart">
    <div class="row row-cart">
      <div class="col-md-12">
        @if (! $cart)
        <div class="alert alert-danger hidden">
          <h4>Error</h4>Sorry, there is something wrong with your cart. Please try again.
        </div>
        @else
        <div>
          <h3>Your Cart</h3>
          <div class="alert alert-dismissable alert-warning hidden">
            <button type="button" class="close" data-dismiss="alert" aria-hidden="true"> × </button>
            <h4>Sorry</h4>[[[ $message ]]]
          </div>

          @if ($cart->cartItems)
          Items in cart: [[ $cart->itemcount() ]]
          <table class="table table-bordered cart-items">
            <thead>
              <tr>
                <th>Code</th>
                <th>Product</th>
                <th>Quantity</th>
                <th class="cart-actions">Action</th>
              </tr>
            </thead>
            <tbody>
              @foreach ($cart->cartItems as $item)
              <tr>
                <td>{{{$item->skucode}}}</td>
                <td>{{{$item->productname}}}</td>
                <td>{{{$item->quantity}}}</td>
                <td><a href="/product-store/remove-from-cart/[[$item->id]]">remove</a></td>
              </tr>
              @endforeach
            </tbody>
          </table>
          <a href="/product-store/clear-cart" class="btn btn-danger">Clear Cart</a>
          <a href="/product-store/checkout-cart" class="btn btn-primary">Checkout</a>
          @else
          <div class="alert alert-info">
            No items in cart.
          </div>
        </div>
        @endif
        @endif
      </div>
    </div>
  </xsl:template>

</xsl:stylesheet>]]></stylesheet>
